module CoDeBruijn where

open import Data.Bool hiding (T)
open import Data.Fin
open import Data.Nat
open import Data.Product
open import Relation.Binary.PropositionalEquality

private
  variable
    i j m n o : â„•

infix  4 _âŠ‘_ _â‡‘_ _â†‘_ _â€¼_
infixr 5 _â¨Ÿ_ _âˆ·âŸ¨_â†‘_âŸ©
infixr 6 Æ›_
infixl 8 _Â·_ _Â·á¶œ_ _Â·áµ›_
infixr 9 T_ K_ L_ R_ B_

--------------------------------------------------------------------------------
-- Thinning and covering

data _âŠ‘_ : â„• â†’ â„• â†’ Set where
  âˆ™  : 0 âŠ‘ 0
  T_ : m âŠ‘ n â†’ m âŠ‘ suc n -- thin
  K_ : m âŠ‘ n â†’ suc m âŠ‘ suc n -- keep

data Cover : â„• â†’ â„• â†’ â„• â†’ Set where
  âˆ™  : Cover 0 0 0
  L_ : Cover m n o â†’ Cover (suc m) n (suc o) -- left
  R_ : Cover m n o â†’ Cover m (suc n) (suc o) -- right
  B_ : Cover m n o â†’ Cover (suc m) (suc n) (suc o) -- both

-- Thinned thing
record _â‡‘_ (S : â„• â†’ Set) (m : â„•) : Set where
  constructor _â†‘_
  field
    {support} : â„•
    thing     : S support
    thinnning : support âŠ‘ m

open _â‡‘_ public

âˆ™âŠ‘ : 0 âŠ‘ m
âˆ™âŠ‘ {zero}  = âˆ™
âˆ™âŠ‘ {suc m} = T âˆ™âŠ‘

idâŠ‘ : âˆ€ {m} â†’ m âŠ‘ m
idâŠ‘ {zero}  = âˆ™
idâŠ‘ {suc m} = K idâŠ‘

_â¨Ÿ_ : m âŠ‘ n â†’ n âŠ‘ o â†’ m âŠ‘ o
Î¸   â¨Ÿ âˆ™   = Î¸
Î¸   â¨Ÿ T Ï† = T (Î¸ â¨Ÿ Ï†)
T Î¸ â¨Ÿ K Ï† = T (Î¸ â¨Ÿ Ï†)
K Î¸ â¨Ÿ K Ï† = K (Î¸ â¨Ÿ Ï†)

thinâ‡‘ : {S : â„• â†’ Set} â†’ m âŠ‘ n â†’ S â‡‘ m â†’ S â‡‘ n
thinâ‡‘ Î¸ (x â†‘ Ï†) = x â†‘ Ï† â¨Ÿ Î¸

thinL : Cover m n o â†’ m âŠ‘ o
thinL âˆ™     = âˆ™
thinL (L c) = K thinL c
thinL (R c) = T thinL c
thinL (B c) = K thinL c

thinR : Cover m n o â†’ n âŠ‘ o
thinR âˆ™     = âˆ™
thinR (L c) = T thinR c
thinR (R c) = K thinR c
thinR (B c) = K thinR c

coprod : i âŠ‘ m â†’ j âŠ‘ m â†’ âˆƒ[ n ] Cover i j n Ã— n âŠ‘ m
coprod âˆ™        âˆ™  = 0 , âˆ™ , âˆ™
coprod (T Î¸) (T Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in -,   c , T Ïˆ
coprod (T Î¸) (K Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in -, R c , K Ïˆ
coprod (K Î¸) (T Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in -, L c , K Ïˆ
coprod (K Î¸) (K Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in -, B c , K Ïˆ

--------------------------------------------------------------------------------
-- Lambda terms with co-de Bruijn indices

sucIf : Bool â†’ â„• â†’ â„•
sucIf b n = if b then suc n else n

data Termâ€² : â„• â†’ Set where
  var : Termâ€² 1
  lam : (use : Bool) â†’ Termâ€² (sucIf use n) â†’ Termâ€² n
  app : Cover m n o â†’ Termâ€² m â†’ Termâ€² n â†’ Termâ€² o

Term = Termâ€² â‡‘_

varâ€² : 1 âŠ‘ n â†’ Term n
varâ€² i = var â†‘ i

Æ›_ : Term (suc n) â†’ Term n
Æ›_ (t â†‘ T Î¸) = lam false t â†‘ Î¸
Æ›_ (t â†‘ K Î¸) = lam true  t â†‘ Î¸

_Â·_ : Term n â†’ Term n â†’ Term n
(t â†‘ Î¸) Â· (u â†‘ Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in app c t u â†‘ Ïˆ

--------------------------------------------------------------------------------
-- Values

mutual

  data Valâ€² : â„• â†’ Set where
    lam   : Closure m â†’ Valâ€² m
    rigid : Spine m â†’ Valâ€² m

  data Closure : â„• â†’ Set where
    clos : m âŠ‘ n â†’ Env o n â†’ (use : Bool) â†’ Termâ€² (sucIf use m) â†’ Closure o

  data Spine : â„• â†’ Set where
    âˆ™   : Spine 1
    app : Cover m n o â†’ Spine m â†’ Valâ€² n â†’ Spine o

  data Env : â„• â†’ â„• â†’ Set where
    âˆ™       : Env m 0
    _âˆ·âŸ¨_â†‘_âŸ© : Val m â†’ Env n o â†’ n âŠ‘ m â†’ Env m (suc o)

  Val = Valâ€² â‡‘_

thinEnv : m âŠ‘ n â†’ Env m o â†’ Env n o
thinEnv _ âˆ™              = âˆ™
thinEnv Î¸ (t âˆ·âŸ¨ Ï â†‘ Ï† âŸ©) = thinâ‡‘ Î¸ t âˆ·âŸ¨ Ï â†‘ Ï† â¨Ÿ Î¸ âŸ©

_â€¼_ : Env m n â†’ 1 âŠ‘ n â†’ Val m
_ âˆ·âŸ¨ Ï â†‘ Ï† âŸ© â€¼ T Î¸ = thinâ‡‘ Ï† (Ï â€¼ Î¸)
t âˆ·âŸ¨ _ â†‘ _ âŸ© â€¼ K Î¸ = t

--------------------------------------------------------------------------------
-- Evaluation and read-back

{-# TERMINATING #-}
mutual

  eval : Env m n â†’ Term n â†’ Val m
  eval Ï (var       â†‘ Î¸) = Ï â€¼ Î¸
  eval Ï (lam Ï† t   â†‘ Î¸) = lam (clos Î¸ Ï Ï† t) â†‘ idâŠ‘
  eval Ï (app c t u â†‘ Î¸) = eval Ï (t â†‘ thinL c â¨Ÿ Î¸) Â·áµ› eval Ï (u â†‘ thinR c â¨Ÿ Î¸)

  _Â·á¶œ_ : Closure â‡‘ m â†’ Val m â†’ Val m
  (clos Î¸ Ï false t â†‘ Ï†) Â·á¶œ m = eval (thinEnv Ï† Ï) (t â†‘ Î¸)
  (clos Î¸ Ï true  t â†‘ Ï†) Â·á¶œ m = eval (m âˆ·âŸ¨ Ï â†‘ Ï† âŸ©) (t â†‘ K Î¸)

  _Â·áµ›_ : Val m â†’ Val m â†’ Val m
  (rigid sp â†‘ Î¸) Â·áµ› (u â†‘ Ï†) = let _ , c , Ïˆ = coprod Î¸ Ï† in rigid (app c sp u) â†‘ Ïˆ
  (lam cl   â†‘ Î¸) Â·áµ› u       = (cl â†‘ Î¸) Â·á¶œ u

{-# TERMINATING #-}
mutual

  readBack : Val m â†’ Term m
  readBack (lam cl   â†‘ Î¸) = Æ› readBack ((cl â†‘ T Î¸) Â·á¶œ (rigid âˆ™ â†‘ K âˆ™âŠ‘))
  readBack (rigid sp â†‘ Î¸) = readBackSpine (sp â†‘ Î¸)

  readBackSpine : Spine â‡‘ m â†’ Term m
  readBackSpine (âˆ™          â†‘ Î¸) = var â†‘ (K âˆ™âŠ‘ â¨Ÿ Î¸)
  readBackSpine (app c sp t â†‘ Î¸) = thinâ‡‘ Î¸ (readBackSpine (sp â†‘ thinL c) Â· readBack (t â†‘ thinR c))

nf : Env m n â†’ Term n â†’ Term m
nf Ï t = readBack (eval Ï t)

--------------------------------------------------------------------------------

ğ•€ : Term 0
ğ•€ = Æ› varâ€² (K âˆ™)

ğ•‚ : Term 0
ğ•‚ = Æ› Æ› varâ€² (T K âˆ™)

ğ•Š : Term 0
ğ•Š = Æ› Æ› Æ› varâ€² (T T K âˆ™) Â· varâ€² (K T T âˆ™) Â· (varâ€² (T K T âˆ™) Â· varâ€² (K T T âˆ™))

Châ€² : â„• â†’ Term 2
Châ€² zero    = varâ€² (K T âˆ™)
Châ€² (suc n) = varâ€² (T K âˆ™) Â· Châ€² n

Ch : â„• â†’ Term 0
Ch n = Æ› Æ› Châ€² n

add : Term 0
add = Æ› Æ› Æ› Æ› varâ€² (T T T K âˆ™) Â· varâ€² (T K T T âˆ™) Â· (varâ€² (T T K T âˆ™) Â· varâ€² (T K T T âˆ™) Â· varâ€² (K T T T âˆ™))

_ : ğ•‚ â‰¡ (lam true (lam false var) â†‘ âˆ™)
_ = refl

_ : nf âˆ™ (ğ•Š Â· ğ•‚ Â· ğ•Š Â· ğ•‚) â‰¡ ğ•‚
_ = refl

_ : nf âˆ™ (add Â· Ch 2 Â· Ch 40) â‰¡ Ch 42
_ = refl
